GBYTES* AXR$                 . ALLOW USE OF MNEMONICS
I       DO      034 , ;
	S       I-1,SAVE+I-1 . SAVE ALL A AND X REGISTERS
	LX      X10,A0       . LOAD ADDRESS OF ARGUMENT LIST
	LA      A0,*3,X10    . LOAD SIZE (LENGTH OF BIT STRING)
	TG      A0,MAXSIZ    . CHECK THAT SIZE IS NOT GREATER THAN 36 BITS
	J       0,X11        . SIZE TOO LARGE, EXIT
	LA      A2,*2,X10    . LOAD THE INITIAL OFFSET
	SZ      A1           .
	DI      A1,WRDSIZ    . SEPERATE OFFSET INTO WORDS AND BITS
	LA      A3,*5,X10    . LOAD THE NUMBER OF REPETITIONS
	TNZ     A3           . CHECK REPETITION REQUEST
	J       0,X11        . IF ZERO, EXIT
	LX      X7,0,X10     . LOAD THE ADDRESS OF THE INPUT ARRAY
	AX      X7,A1        . ADD WORD OFFSET TO INPUT ADDRESS
	LX      X8,1,X10     . LOAD THE ADDRESS OF THE OUTPUT ARRAY
	LXI     X8,ONE       . SET THE INCREMENT TO ONE
	LA      A5,*4,X10    . LOAD THE SKIP VALUE
	SZ      A4           .
	DI      A4,WRDSIZ    . SEPERATE SKIP INTO WORDS AND BITS
	LA      A6,SMASK     . LOAD THE FULL WORD MASK
	LX      X6,WRDSIZ    . LOAD THE WORD SIZE
	ANX     X6,A0        . SUBTRACT THE SIZE DESIRED
	SSL     A6,0,X6      . BUILD THE FINAL MASK
LOOP    DL      A7,0,X7      . LOAD TWO WORDS FROM THE INPUT ARRAY
	LX      X6,A2        . LOAD THE BIT OFFSET
	AX      X6,A0        . ADD THE SIZE
	LDSC    A7,0,X6      . DOUBLE SHIFT INTO LOW ORDER POSITION
	AND     A8,A6        . MASK OUT UNWANTED BITS
	SA      A9,0,*X8     . STORE THE RESULT AND INCREMENT ADDRESS
	A       A2,A0        . ADD THE SIZE TO THE BIT OFFSET
	A       A2,A5        . ADD THE SKIP BITS TO THE OFFSET
	TG      A2,WRDSIZ    . CHECK WHETHER OFFSET IS GREATER THAN 36 BITS
	AX      X7,ONE       . IF IT IS, INCREMENT INPUT ADDRESS
	TG      A2,WRDSIZ    . CHECK AGAIN
	AN      A2,WRDSIZ    . IF IT IS, DECREASE OFFSET BY WORD LENGTH
	AX      X7,A4        . INCREMENT INPUT ADDRESS
	AN      A3,ONE       . DECREMENT REPETITION COUNTER BY ONE
	TZ      A3           . CHECK TO SEE IF DONE
	J       LOOP         . NOT DONE, GET ANOTHER STRING
J       DO      034 , ;
	L       J-1,SAVE+J-1 . RESTORE SAVED REGISTERS
	J       0,X11        . DONE, RETURN TO CALLING PROGRAM
.
MAXSIZ  37                   . WORD SIZE + 1 (ALLOWS SIZE = 36 BITS)
WRDSIZ  36                   . MACHINE WORD SIZE
ONE     1                    . CONSTANT 1
SMASK   -0                   . GENERATE WORD OF ALL 1'S FOR MASK
.
SAVE    RES     034          . REGISTER SAVE AREA
SBYTES* AXR$                 . ALLOW USE OF MNEMONICS
I       DO      034 , ;
	S       I-1,SAVE+I-1 . SAVE ALL A AND X REGISTERS
	LX      X10,A0       . LOAD ADDRESS OF ARGUMENT LIST
	LA      A0,*3,X10    . LOAD SIZE (LENGTH OF BIT STRING)
	TG      A0,MAXSIZ    . CHECK THAT SIZE IS NOT GREATER THAN 36 BITS
	J       0,X11        . SIZE TOO LARGE, EXIT
	LA      A2,*2,X10    . LOAD THE INITIAL OFFSET
	SZ      A1           .
	DI      A1,WRDSIZ    . SEPERATE OFFSET INTO WORDS AND BITS
	LA      A3,*5,X10    . LOAD THE NUMBER OF REPETITIONS
	TNZ     A3           . CHECK THE REPETITION REQUEST
	J       0,X11        . IF ZERO, EXIT
	LX      X7,0,X10     . LOAD THE ADDRESS OF THE OUTPUT ARRAY
	AX      X7,A1        . ADD WORD OFFSET TO OUTPUT ADDRESS
	LX      X8,1,X10     . LOAD THE ADDRESS OF THE INPUT ARRAY
	LXI     X8,ONE       . SET THE INCREMENT TO ONE
	LA      A5,*4,X10    . LOAD THE SKIP VALUE
	SZ      A4           .
	DI      A4,WRDSIZ    . SEPERATE SKIP INTO WORDS AND BITS
	LA      A6,SMASK     . LOAD THE FULL WORD MASK
	LX      X6,A0        . LOAD THE SIZE
	LSSL    A6,0,X6      . BUILD MASK (TO CLEAR OUTPUT AREA)
	LA      A9,SMASK     . LOAD THE FULL WORD MASK AGAIN
	XOR     A9,A6        . BUILD INPUT SELECT MASK
LOOP    DL      A7,0,X7      . LOAD TWO WORDS FROM THE OUTPUT ARRAY
	LX      X6,A2        . LOAD THE BIT OFFSET
	AX      X6,A0        . ADD THE SIZE
	LDSC    A7,0,X6      . DOUBLE SHIFT INTO LOW ORDER POSITION
	AND     A8,A6        . CLEAR THE OUTPUT AREA
	AND     A10,0,*X8    . LOAD AND MASK THE INPUT BIT STRING
	OR      A9,A11       . PLACE INPUT BITS INTO OUTPUT AREA
	LA      A8,A10       . PUT NEW WORD BACK FOR SHIFT
	DSC     A7,0,X6      . DOUBLE SHIFT BACK TO ORIGINAL POSITION
	DS      A7,0,X7      . STORE THE RESULT
	A       A2,A0        . ADD THE SIZE TO THE BIT OFFSET
	A       A2,A5        . ADD THE SKIP BITS TO THE OFFSET
	TG      A2,WRDSIZ    . CHECK WHETHER OFFSET IS GREATER THAN 36 BITS
	AX      X7,ONE       . IF IT IS, INCREMENT OUTPUT ADDRESS
	TG      A2,WRDSIZ    . CHECK AGAIN
	AN      A2,WRDSIZ    . IF IT IS, DECREASE OFFSET BY WORD LENGTH
	AX      X7,A4        . INCREMENT OUTPUT ADDRESS
	AN      A3,ONE       . DECREMENT REPETITION COUNTER BY ONE
	TZ      A3           . CHECK TO SEE IF DONE
	J       LOOP         . NOT DONE, GET ANOTHER STRING
J       DO      034 , ;
	L       J-1,SAVE+J-1 . RESTORE SAVED REGISTERS
	J       0,X11        . DONE, RETURN TO CALLING PROGRAM
.
MAXSIZ  37                   . WORD SIZE +1 (ALLOWS SIZE = 36 BITS)
WRDSIZ  36                   . MACHINE WORD SIZE
ONE     1                    . CONSTANT 1
SMASK   -0                   . GENERATE WORD OF ALL 1'S FOR MASK
.
SAVE    RES     034          . REGISTER SAVE AREA
.
	END                  .
